# Contractible maps

<pre class="Agda"><a id="30" class="Keyword">module</a> <a id="37" href="foundation-core.contractible-maps.html" class="Module">foundation-core.contractible-maps</a> <a id="71" class="Keyword">where</a>
</pre>
<details><summary>Imports</summary>

<pre class="Agda"><a id="127" class="Keyword">open</a> <a id="132" class="Keyword">import</a> <a id="139" href="foundation.action-on-identifications-functions.html" class="Module">foundation.action-on-identifications-functions</a>
<a id="186" class="Keyword">open</a> <a id="191" class="Keyword">import</a> <a id="198" href="foundation.dependent-pair-types.html" class="Module">foundation.dependent-pair-types</a>
<a id="230" class="Keyword">open</a> <a id="235" class="Keyword">import</a> <a id="242" href="foundation.universe-levels.html" class="Module">foundation.universe-levels</a>

<a id="270" class="Keyword">open</a> <a id="275" class="Keyword">import</a> <a id="282" href="foundation-core.coherently-invertible-maps.html" class="Module">foundation-core.coherently-invertible-maps</a>
<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="foundation-core.contractible-types.html" class="Module">foundation-core.contractible-types</a>
<a id="372" class="Keyword">open</a> <a id="377" class="Keyword">import</a> <a id="384" href="foundation-core.equivalences.html" class="Module">foundation-core.equivalences</a>
<a id="413" class="Keyword">open</a> <a id="418" class="Keyword">import</a> <a id="425" href="foundation-core.fibers-of-maps.html" class="Module">foundation-core.fibers-of-maps</a>
<a id="456" class="Keyword">open</a> <a id="461" class="Keyword">import</a> <a id="468" href="foundation-core.function-types.html" class="Module">foundation-core.function-types</a>
<a id="499" class="Keyword">open</a> <a id="504" class="Keyword">import</a> <a id="511" href="foundation-core.homotopies.html" class="Module">foundation-core.homotopies</a>
<a id="538" class="Keyword">open</a> <a id="543" class="Keyword">import</a> <a id="550" href="foundation-core.identity-types.html" class="Module">foundation-core.identity-types</a>
</pre>
</details>

## Idea

A map is often said to satisfy a property `P` if each of its fibers satisfy
property `P`. Thus, we define contractible maps to be maps of which each fiber
is contractible. In other words, contractible maps are maps `f : A → B` such
that for each element `b : B` there is a unique `a : A` equipped with an
identification `(f a) ＝ b`, i.e., contractible maps are the type theoretic
bijections.

## Definition

<pre class="Agda"><a id="1023" class="Keyword">module</a> <a id="1030" href="foundation-core.contractible-maps.html#1030" class="Module">_</a>
  <a id="1034" class="Symbol">{</a><a id="1035" href="foundation-core.contractible-maps.html#1035" class="Bound">l1</a> <a id="1038" href="foundation-core.contractible-maps.html#1038" class="Bound">l2</a> <a id="1041" class="Symbol">:</a> <a id="1043" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="1048" class="Symbol">}</a> <a id="1050" class="Symbol">{</a><a id="1051" href="foundation-core.contractible-maps.html#1051" class="Bound">A</a> <a id="1053" class="Symbol">:</a> <a id="1055" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="1058" href="foundation-core.contractible-maps.html#1035" class="Bound">l1</a><a id="1060" class="Symbol">}</a> <a id="1062" class="Symbol">{</a><a id="1063" href="foundation-core.contractible-maps.html#1063" class="Bound">B</a> <a id="1065" class="Symbol">:</a> <a id="1067" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="1070" href="foundation-core.contractible-maps.html#1038" class="Bound">l2</a><a id="1072" class="Symbol">}</a>
  <a id="1076" class="Keyword">where</a>

  <a id="1085" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="1098" class="Symbol">:</a> <a id="1100" class="Symbol">(</a><a id="1101" href="foundation-core.contractible-maps.html#1051" class="Bound">A</a> <a id="1103" class="Symbol">→</a> <a id="1105" href="foundation-core.contractible-maps.html#1063" class="Bound">B</a><a id="1106" class="Symbol">)</a> <a id="1108" class="Symbol">→</a> <a id="1110" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="1113" class="Symbol">(</a><a id="1114" href="foundation-core.contractible-maps.html#1035" class="Bound">l1</a> <a id="1117" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1119" href="foundation-core.contractible-maps.html#1038" class="Bound">l2</a><a id="1121" class="Symbol">)</a>
  <a id="1125" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="1138" href="foundation-core.contractible-maps.html#1138" class="Bound">f</a> <a id="1140" class="Symbol">=</a> <a id="1142" class="Symbol">(</a><a id="1143" href="foundation-core.contractible-maps.html#1143" class="Bound">y</a> <a id="1145" class="Symbol">:</a> <a id="1147" href="foundation-core.contractible-maps.html#1063" class="Bound">B</a><a id="1148" class="Symbol">)</a> <a id="1150" class="Symbol">→</a> <a id="1152" href="foundation-core.contractible-types.html#802" class="Function">is-contr</a> <a id="1161" class="Symbol">(</a><a id="1162" href="foundation-core.fibers-of-maps.html#867" class="Function">fiber</a> <a id="1168" href="foundation-core.contractible-maps.html#1138" class="Bound">f</a> <a id="1170" href="foundation-core.contractible-maps.html#1143" class="Bound">y</a><a id="1171" class="Symbol">)</a>
</pre>
## Properties

### Any contractible map is an equivalence

<pre class="Agda"><a id="1245" class="Keyword">module</a> <a id="1252" href="foundation-core.contractible-maps.html#1252" class="Module">_</a>
  <a id="1256" class="Symbol">{</a><a id="1257" href="foundation-core.contractible-maps.html#1257" class="Bound">l1</a> <a id="1260" href="foundation-core.contractible-maps.html#1260" class="Bound">l2</a> <a id="1263" class="Symbol">:</a> <a id="1265" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="1270" class="Symbol">}</a> <a id="1272" class="Symbol">{</a><a id="1273" href="foundation-core.contractible-maps.html#1273" class="Bound">A</a> <a id="1275" class="Symbol">:</a> <a id="1277" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="1280" href="foundation-core.contractible-maps.html#1257" class="Bound">l1</a><a id="1282" class="Symbol">}</a> <a id="1284" class="Symbol">{</a><a id="1285" href="foundation-core.contractible-maps.html#1285" class="Bound">B</a> <a id="1287" class="Symbol">:</a> <a id="1289" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="1292" href="foundation-core.contractible-maps.html#1260" class="Bound">l2</a><a id="1294" class="Symbol">}</a> <a id="1296" class="Symbol">{</a><a id="1297" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1299" class="Symbol">:</a> <a id="1301" href="foundation-core.contractible-maps.html#1273" class="Bound">A</a> <a id="1303" class="Symbol">→</a> <a id="1305" href="foundation-core.contractible-maps.html#1285" class="Bound">B</a><a id="1306" class="Symbol">}</a>
  <a id="1310" class="Keyword">where</a>

  <a id="1319" href="foundation-core.contractible-maps.html#1319" class="Function">map-inv-is-contr-map</a> <a id="1340" class="Symbol">:</a> <a id="1342" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="1355" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1357" class="Symbol">→</a> <a id="1359" href="foundation-core.contractible-maps.html#1285" class="Bound">B</a> <a id="1361" class="Symbol">→</a> <a id="1363" href="foundation-core.contractible-maps.html#1273" class="Bound">A</a>
  <a id="1367" href="foundation-core.contractible-maps.html#1319" class="Function">map-inv-is-contr-map</a> <a id="1388" href="foundation-core.contractible-maps.html#1388" class="Bound">H</a> <a id="1390" href="foundation-core.contractible-maps.html#1390" class="Bound">y</a> <a id="1392" class="Symbol">=</a> <a id="1394" href="foundation.dependent-pair-types.html#603" class="Field">pr1</a> <a id="1398" class="Symbol">(</a><a id="1399" href="foundation-core.contractible-types.html#894" class="Function">center</a> <a id="1406" class="Symbol">(</a><a id="1407" href="foundation-core.contractible-maps.html#1388" class="Bound">H</a> <a id="1409" href="foundation-core.contractible-maps.html#1390" class="Bound">y</a><a id="1410" class="Symbol">))</a>

  <a id="1416" href="foundation-core.contractible-maps.html#1416" class="Function">is-section-map-inv-is-contr-map</a> <a id="1448" class="Symbol">:</a>
    <a id="1454" class="Symbol">(</a><a id="1455" href="foundation-core.contractible-maps.html#1455" class="Bound">H</a> <a id="1457" class="Symbol">:</a> <a id="1459" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="1472" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a><a id="1473" class="Symbol">)</a> <a id="1475" class="Symbol">→</a> <a id="1477" class="Symbol">(</a><a id="1478" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1480" href="foundation-core.function-types.html#455" class="Function Operator">∘</a> <a id="1482" class="Symbol">(</a><a id="1483" href="foundation-core.contractible-maps.html#1319" class="Function">map-inv-is-contr-map</a> <a id="1504" href="foundation-core.contractible-maps.html#1455" class="Bound">H</a><a id="1505" class="Symbol">))</a> <a id="1508" href="foundation-core.homotopies.html#2717" class="Function Operator">~</a> <a id="1510" href="foundation-core.function-types.html#307" class="Function">id</a>
  <a id="1515" href="foundation-core.contractible-maps.html#1416" class="Function">is-section-map-inv-is-contr-map</a> <a id="1547" href="foundation-core.contractible-maps.html#1547" class="Bound">H</a> <a id="1549" href="foundation-core.contractible-maps.html#1549" class="Bound">y</a> <a id="1551" class="Symbol">=</a> <a id="1553" href="foundation.dependent-pair-types.html#615" class="Field">pr2</a> <a id="1557" class="Symbol">(</a><a id="1558" href="foundation-core.contractible-types.html#894" class="Function">center</a> <a id="1565" class="Symbol">(</a><a id="1566" href="foundation-core.contractible-maps.html#1547" class="Bound">H</a> <a id="1568" href="foundation-core.contractible-maps.html#1549" class="Bound">y</a><a id="1569" class="Symbol">))</a>

  <a id="1575" href="foundation-core.contractible-maps.html#1575" class="Function">is-retraction-map-inv-is-contr-map</a> <a id="1610" class="Symbol">:</a>
    <a id="1616" class="Symbol">(</a><a id="1617" href="foundation-core.contractible-maps.html#1617" class="Bound">H</a> <a id="1619" class="Symbol">:</a> <a id="1621" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="1634" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a><a id="1635" class="Symbol">)</a> <a id="1637" class="Symbol">→</a> <a id="1639" class="Symbol">((</a><a id="1641" href="foundation-core.contractible-maps.html#1319" class="Function">map-inv-is-contr-map</a> <a id="1662" href="foundation-core.contractible-maps.html#1617" class="Bound">H</a><a id="1663" class="Symbol">)</a> <a id="1665" href="foundation-core.function-types.html#455" class="Function Operator">∘</a> <a id="1667" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a><a id="1668" class="Symbol">)</a> <a id="1670" href="foundation-core.homotopies.html#2717" class="Function Operator">~</a> <a id="1672" href="foundation-core.function-types.html#307" class="Function">id</a>
  <a id="1677" href="foundation-core.contractible-maps.html#1575" class="Function">is-retraction-map-inv-is-contr-map</a> <a id="1712" href="foundation-core.contractible-maps.html#1712" class="Bound">H</a> <a id="1714" href="foundation-core.contractible-maps.html#1714" class="Bound">x</a> <a id="1716" class="Symbol">=</a>
    <a id="1722" href="foundation.action-on-identifications-functions.html#790" class="Function">ap</a>
      <a id="1731" class="Symbol">(</a> <a id="1733" href="foundation.dependent-pair-types.html#603" class="Field">pr1</a> <a id="1737" class="Symbol">{</a><a id="1738" class="Argument">B</a> <a id="1740" class="Symbol">=</a> <a id="1742" class="Symbol">λ</a> <a id="1744" href="foundation-core.contractible-maps.html#1744" class="Bound">z</a> <a id="1746" class="Symbol">→</a> <a id="1748" class="Symbol">(</a><a id="1749" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1751" href="foundation-core.contractible-maps.html#1744" class="Bound">z</a><a id="1752" class="Symbol">)</a> <a id="1754" href="foundation-core.identity-types.html#5999" class="Function Operator">＝</a> <a id="1756" class="Symbol">(</a><a id="1757" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1759" href="foundation-core.contractible-maps.html#1714" class="Bound">x</a><a id="1760" class="Symbol">)})</a>
      <a id="1770" class="Symbol">(</a> <a id="1772" class="Symbol">(</a> <a id="1774" href="foundation-core.identity-types.html#7252" class="Function">inv</a>
          <a id="1788" class="Symbol">(</a> <a id="1790" href="foundation-core.contractible-types.html#1232" class="Function">contraction</a>
            <a id="1814" class="Symbol">(</a> <a id="1816" href="foundation-core.contractible-maps.html#1712" class="Bound">H</a> <a id="1818" class="Symbol">(</a><a id="1819" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1821" href="foundation-core.contractible-maps.html#1714" class="Bound">x</a><a id="1822" class="Symbol">))</a>
            <a id="1837" class="Symbol">(</a> <a id="1839" href="foundation.dependent-pair-types.html#586" class="InductiveConstructor">pair</a>
              <a id="1858" class="Symbol">(</a> <a id="1860" href="foundation-core.contractible-maps.html#1319" class="Function">map-inv-is-contr-map</a> <a id="1881" href="foundation-core.contractible-maps.html#1712" class="Bound">H</a> <a id="1883" class="Symbol">(</a><a id="1884" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1886" href="foundation-core.contractible-maps.html#1714" class="Bound">x</a><a id="1887" class="Symbol">))</a>
              <a id="1904" class="Symbol">(</a> <a id="1906" href="foundation-core.contractible-maps.html#1416" class="Function">is-section-map-inv-is-contr-map</a> <a id="1938" href="foundation-core.contractible-maps.html#1712" class="Bound">H</a> <a id="1940" class="Symbol">(</a><a id="1941" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1943" href="foundation-core.contractible-maps.html#1714" class="Bound">x</a><a id="1944" class="Symbol">)))))</a> <a id="1950" href="foundation-core.identity-types.html#6948" class="Function Operator">∙</a>
        <a id="1960" class="Symbol">(</a> <a id="1962" href="foundation-core.contractible-types.html#1232" class="Function">contraction</a> <a id="1974" class="Symbol">(</a><a id="1975" href="foundation-core.contractible-maps.html#1712" class="Bound">H</a> <a id="1977" class="Symbol">(</a><a id="1978" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="1980" href="foundation-core.contractible-maps.html#1714" class="Bound">x</a><a id="1981" class="Symbol">))</a> <a id="1984" class="Symbol">(</a><a id="1985" href="foundation.dependent-pair-types.html#586" class="InductiveConstructor">pair</a> <a id="1990" href="foundation-core.contractible-maps.html#1714" class="Bound">x</a> <a id="1992" href="foundation-core.identity-types.html#5968" class="InductiveConstructor">refl</a><a id="1996" class="Symbol">)))</a>

  <a id="2003" class="Keyword">abstract</a>
    <a id="2016" href="foundation-core.contractible-maps.html#2016" class="Function">is-equiv-is-contr-map</a> <a id="2038" class="Symbol">:</a> <a id="2040" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="2053" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a> <a id="2055" class="Symbol">→</a> <a id="2057" href="foundation-core.equivalences.html#1353" class="Function">is-equiv</a> <a id="2066" href="foundation-core.contractible-maps.html#1297" class="Bound">f</a>
    <a id="2072" href="foundation-core.contractible-maps.html#2016" class="Function">is-equiv-is-contr-map</a> <a id="2094" href="foundation-core.contractible-maps.html#2094" class="Bound">H</a> <a id="2096" class="Symbol">=</a>
      <a id="2104" href="foundation-core.equivalences.html#4422" class="Function">is-equiv-is-invertible</a>
        <a id="2135" class="Symbol">(</a> <a id="2137" href="foundation-core.contractible-maps.html#1319" class="Function">map-inv-is-contr-map</a> <a id="2158" href="foundation-core.contractible-maps.html#2094" class="Bound">H</a><a id="2159" class="Symbol">)</a>
        <a id="2169" class="Symbol">(</a> <a id="2171" href="foundation-core.contractible-maps.html#1416" class="Function">is-section-map-inv-is-contr-map</a> <a id="2203" href="foundation-core.contractible-maps.html#2094" class="Bound">H</a><a id="2204" class="Symbol">)</a>
        <a id="2214" class="Symbol">(</a> <a id="2216" href="foundation-core.contractible-maps.html#1575" class="Function">is-retraction-map-inv-is-contr-map</a> <a id="2251" href="foundation-core.contractible-maps.html#2094" class="Bound">H</a><a id="2252" class="Symbol">)</a>
</pre>
### Any coherently invertible map is a contractible map

<pre class="Agda"><a id="2324" class="Keyword">module</a> <a id="2331" href="foundation-core.contractible-maps.html#2331" class="Module">_</a>
  <a id="2335" class="Symbol">{</a><a id="2336" href="foundation-core.contractible-maps.html#2336" class="Bound">l1</a> <a id="2339" href="foundation-core.contractible-maps.html#2339" class="Bound">l2</a> <a id="2342" class="Symbol">:</a> <a id="2344" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="2349" class="Symbol">}</a> <a id="2351" class="Symbol">{</a><a id="2352" href="foundation-core.contractible-maps.html#2352" class="Bound">A</a> <a id="2354" class="Symbol">:</a> <a id="2356" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="2359" href="foundation-core.contractible-maps.html#2336" class="Bound">l1</a><a id="2361" class="Symbol">}</a> <a id="2363" class="Symbol">{</a><a id="2364" href="foundation-core.contractible-maps.html#2364" class="Bound">B</a> <a id="2366" class="Symbol">:</a> <a id="2368" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="2371" href="foundation-core.contractible-maps.html#2339" class="Bound">l2</a><a id="2373" class="Symbol">}</a> <a id="2375" class="Symbol">{</a><a id="2376" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a> <a id="2378" class="Symbol">:</a> <a id="2380" href="foundation-core.contractible-maps.html#2352" class="Bound">A</a> <a id="2382" class="Symbol">→</a> <a id="2384" href="foundation-core.contractible-maps.html#2364" class="Bound">B</a><a id="2385" class="Symbol">}</a>
  <a id="2389" class="Keyword">where</a>

  <a id="2398" class="Keyword">abstract</a>
    <a id="2411" href="foundation-core.contractible-maps.html#2411" class="Function">center-fiber-is-coherently-invertible</a> <a id="2449" class="Symbol">:</a>
      <a id="2457" href="foundation-core.coherently-invertible-maps.html#1517" class="Function">is-coherently-invertible</a> <a id="2482" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a> <a id="2484" class="Symbol">→</a> <a id="2486" class="Symbol">(</a><a id="2487" href="foundation-core.contractible-maps.html#2487" class="Bound">y</a> <a id="2489" class="Symbol">:</a> <a id="2491" href="foundation-core.contractible-maps.html#2364" class="Bound">B</a><a id="2492" class="Symbol">)</a> <a id="2494" class="Symbol">→</a> <a id="2496" href="foundation-core.fibers-of-maps.html#867" class="Function">fiber</a> <a id="2502" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a> <a id="2504" href="foundation-core.contractible-maps.html#2487" class="Bound">y</a>
    <a id="2510" href="foundation.dependent-pair-types.html#603" class="Field">pr1</a> <a id="2514" class="Symbol">(</a><a id="2515" href="foundation-core.contractible-maps.html#2411" class="Function">center-fiber-is-coherently-invertible</a> <a id="2553" href="foundation-core.contractible-maps.html#2553" class="Bound">H</a> <a id="2555" href="foundation-core.contractible-maps.html#2555" class="Bound">y</a><a id="2556" class="Symbol">)</a> <a id="2558" class="Symbol">=</a>
      <a id="2566" href="foundation-core.coherently-invertible-maps.html#1851" class="Function">map-inv-is-coherently-invertible</a> <a id="2599" href="foundation-core.contractible-maps.html#2553" class="Bound">H</a> <a id="2601" href="foundation-core.contractible-maps.html#2555" class="Bound">y</a>
    <a id="2607" href="foundation.dependent-pair-types.html#615" class="Field">pr2</a> <a id="2611" class="Symbol">(</a><a id="2612" href="foundation-core.contractible-maps.html#2411" class="Function">center-fiber-is-coherently-invertible</a> <a id="2650" href="foundation-core.contractible-maps.html#2650" class="Bound">H</a> <a id="2652" href="foundation-core.contractible-maps.html#2652" class="Bound">y</a><a id="2653" class="Symbol">)</a> <a id="2655" class="Symbol">=</a>
      <a id="2663" href="foundation-core.coherently-invertible-maps.html#1938" class="Function">is-retraction-is-coherently-invertible</a> <a id="2702" href="foundation-core.contractible-maps.html#2650" class="Bound">H</a> <a id="2704" href="foundation-core.contractible-maps.html#2652" class="Bound">y</a>

    <a id="2711" href="foundation-core.contractible-maps.html#2711" class="Function">contraction-fiber-is-coherently-invertible</a> <a id="2754" class="Symbol">:</a>
      <a id="2762" class="Symbol">(</a><a id="2763" href="foundation-core.contractible-maps.html#2763" class="Bound">H</a> <a id="2765" class="Symbol">:</a> <a id="2767" href="foundation-core.coherently-invertible-maps.html#1517" class="Function">is-coherently-invertible</a> <a id="2792" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a><a id="2793" class="Symbol">)</a> <a id="2795" class="Symbol">→</a> <a id="2797" class="Symbol">(</a><a id="2798" href="foundation-core.contractible-maps.html#2798" class="Bound">y</a> <a id="2800" class="Symbol">:</a> <a id="2802" href="foundation-core.contractible-maps.html#2364" class="Bound">B</a><a id="2803" class="Symbol">)</a> <a id="2805" class="Symbol">→</a> <a id="2807" class="Symbol">(</a><a id="2808" href="foundation-core.contractible-maps.html#2808" class="Bound">t</a> <a id="2810" class="Symbol">:</a> <a id="2812" href="foundation-core.fibers-of-maps.html#867" class="Function">fiber</a> <a id="2818" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a> <a id="2820" href="foundation-core.contractible-maps.html#2798" class="Bound">y</a><a id="2821" class="Symbol">)</a> <a id="2823" class="Symbol">→</a>
      <a id="2831" class="Symbol">(</a><a id="2832" href="foundation-core.contractible-maps.html#2411" class="Function">center-fiber-is-coherently-invertible</a> <a id="2870" href="foundation-core.contractible-maps.html#2763" class="Bound">H</a> <a id="2872" href="foundation-core.contractible-maps.html#2798" class="Bound">y</a><a id="2873" class="Symbol">)</a> <a id="2875" href="foundation-core.identity-types.html#5999" class="Function Operator">＝</a> <a id="2877" href="foundation-core.contractible-maps.html#2808" class="Bound">t</a>
    <a id="2883" href="foundation-core.contractible-maps.html#2711" class="Function">contraction-fiber-is-coherently-invertible</a> <a id="2926" href="foundation-core.contractible-maps.html#2926" class="Bound">H</a> <a id="2928" href="foundation-core.contractible-maps.html#2928" class="Bound">y</a> <a id="2930" class="Symbol">(</a><a id="2931" href="foundation.dependent-pair-types.html#586" class="InductiveConstructor">pair</a> <a id="2936" href="foundation-core.contractible-maps.html#2936" class="Bound">x</a> <a id="2938" href="foundation-core.identity-types.html#5968" class="InductiveConstructor">refl</a><a id="2942" class="Symbol">)</a> <a id="2944" class="Symbol">=</a>
      <a id="2952" href="foundation-core.fibers-of-maps.html#1622" class="Function">eq-Eq-fiber</a> <a id="2964" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a> <a id="2966" href="foundation-core.contractible-maps.html#2928" class="Bound">y</a>
        <a id="2976" class="Symbol">(</a> <a id="2978" href="foundation-core.coherently-invertible-maps.html#2085" class="Function">is-section-is-coherently-invertible</a> <a id="3014" href="foundation-core.contractible-maps.html#2926" class="Bound">H</a> <a id="3016" href="foundation-core.contractible-maps.html#2936" class="Bound">x</a><a id="3017" class="Symbol">)</a>
        <a id="3027" class="Symbol">(</a> <a id="3029" class="Symbol">(</a> <a id="3031" href="foundation-core.identity-types.html#7593" class="Function">right-unit</a><a id="3041" class="Symbol">)</a> <a id="3043" href="foundation-core.identity-types.html#6948" class="Function Operator">∙</a>
          <a id="3055" class="Symbol">(</a> <a id="3057" href="foundation-core.identity-types.html#7252" class="Function">inv</a> <a id="3061" class="Symbol">(</a> <a id="3063" href="foundation-core.coherently-invertible-maps.html#2232" class="Function">coh-is-coherently-invertible</a> <a id="3092" href="foundation-core.contractible-maps.html#2926" class="Bound">H</a> <a id="3094" href="foundation-core.contractible-maps.html#2936" class="Bound">x</a><a id="3095" class="Symbol">)))</a>

  <a id="3102" href="foundation-core.contractible-maps.html#3102" class="Function">is-contr-map-is-coherently-invertible</a> <a id="3140" class="Symbol">:</a>
    <a id="3146" href="foundation-core.coherently-invertible-maps.html#1517" class="Function">is-coherently-invertible</a> <a id="3171" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a> <a id="3173" class="Symbol">→</a> <a id="3175" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="3188" href="foundation-core.contractible-maps.html#2376" class="Bound">f</a>
  <a id="3192" href="foundation.dependent-pair-types.html#603" class="Field">pr1</a> <a id="3196" class="Symbol">(</a><a id="3197" href="foundation-core.contractible-maps.html#3102" class="Function">is-contr-map-is-coherently-invertible</a> <a id="3235" href="foundation-core.contractible-maps.html#3235" class="Bound">H</a> <a id="3237" href="foundation-core.contractible-maps.html#3237" class="Bound">y</a><a id="3238" class="Symbol">)</a> <a id="3240" class="Symbol">=</a>
    <a id="3246" href="foundation-core.contractible-maps.html#2411" class="Function">center-fiber-is-coherently-invertible</a> <a id="3284" href="foundation-core.contractible-maps.html#3235" class="Bound">H</a> <a id="3286" href="foundation-core.contractible-maps.html#3237" class="Bound">y</a>
  <a id="3290" href="foundation.dependent-pair-types.html#615" class="Field">pr2</a> <a id="3294" class="Symbol">(</a><a id="3295" href="foundation-core.contractible-maps.html#3102" class="Function">is-contr-map-is-coherently-invertible</a> <a id="3333" href="foundation-core.contractible-maps.html#3333" class="Bound">H</a> <a id="3335" href="foundation-core.contractible-maps.html#3335" class="Bound">y</a><a id="3336" class="Symbol">)</a> <a id="3338" class="Symbol">=</a>
    <a id="3344" href="foundation-core.contractible-maps.html#2711" class="Function">contraction-fiber-is-coherently-invertible</a> <a id="3387" href="foundation-core.contractible-maps.html#3333" class="Bound">H</a> <a id="3389" href="foundation-core.contractible-maps.html#3335" class="Bound">y</a>
</pre>
### Any equivalence is a contractible map

<pre class="Agda"><a id="3447" class="Keyword">module</a> <a id="3454" href="foundation-core.contractible-maps.html#3454" class="Module">_</a>
  <a id="3458" class="Symbol">{</a><a id="3459" href="foundation-core.contractible-maps.html#3459" class="Bound">l1</a> <a id="3462" href="foundation-core.contractible-maps.html#3462" class="Bound">l2</a> <a id="3465" class="Symbol">:</a> <a id="3467" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="3472" class="Symbol">}</a> <a id="3474" class="Symbol">{</a><a id="3475" href="foundation-core.contractible-maps.html#3475" class="Bound">A</a> <a id="3477" class="Symbol">:</a> <a id="3479" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="3482" href="foundation-core.contractible-maps.html#3459" class="Bound">l1</a><a id="3484" class="Symbol">}</a> <a id="3486" class="Symbol">{</a><a id="3487" href="foundation-core.contractible-maps.html#3487" class="Bound">B</a> <a id="3489" class="Symbol">:</a> <a id="3491" href="Agda.Primitive.html#320" class="Primitive">UU</a> <a id="3494" href="foundation-core.contractible-maps.html#3462" class="Bound">l2</a><a id="3496" class="Symbol">}</a> <a id="3498" class="Symbol">{</a><a id="3499" href="foundation-core.contractible-maps.html#3499" class="Bound">f</a> <a id="3501" class="Symbol">:</a> <a id="3503" href="foundation-core.contractible-maps.html#3475" class="Bound">A</a> <a id="3505" class="Symbol">→</a> <a id="3507" href="foundation-core.contractible-maps.html#3487" class="Bound">B</a><a id="3508" class="Symbol">}</a>
  <a id="3512" class="Keyword">where</a>

  <a id="3521" class="Keyword">abstract</a>
    <a id="3534" href="foundation-core.contractible-maps.html#3534" class="Function">is-contr-map-is-equiv</a> <a id="3556" class="Symbol">:</a> <a id="3558" href="foundation-core.equivalences.html#1353" class="Function">is-equiv</a> <a id="3567" href="foundation-core.contractible-maps.html#3499" class="Bound">f</a> <a id="3569" class="Symbol">→</a> <a id="3571" href="foundation-core.contractible-maps.html#1085" class="Function">is-contr-map</a> <a id="3584" href="foundation-core.contractible-maps.html#3499" class="Bound">f</a>
    <a id="3590" href="foundation-core.contractible-maps.html#3534" class="Function">is-contr-map-is-equiv</a> <a id="3612" class="Symbol">=</a>
      <a id="3620" href="foundation-core.contractible-maps.html#3102" class="Function">is-contr-map-is-coherently-invertible</a> <a id="3658" href="foundation-core.function-types.html#455" class="Function Operator">∘</a> <a id="3660" href="foundation-core.equivalences.html#5053" class="Function">is-coherently-invertible-is-equiv</a>
</pre>
## See also

- For the notion of biinvertible maps see
  [`foundation.equivalences`](foundation.equivalences.md).
- For the notions of inverses and coherently invertible maps, also known as
  half-adjoint equivalences, see
  [`foundation.coherently-invertible-maps`](foundation.coherently-invertible-maps.md).
- For the notion of path-split maps see
  [`foundation.path-split-maps`](foundation.path-split-maps.md).
